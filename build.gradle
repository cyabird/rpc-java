plugins {
    id "java"
    id "com.gradle.build-scan" version "1.11"
}

// 审视扫描
buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

ext {
    moduleProjects = subprojects.findAll {
        !it.name.equals('rpc-documentation')
    }
}

// 所有项目配置
configure(allprojects) { project ->
    group = "org.cyabird"
    version = '1.0-SNAPSHOT'

    apply plugin: "java"

    repositories {
        mavenCentral()
        mavenLocal()
    }

    configurations.all {
        // 检查更新时间
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

// 模块项目
configure(moduleProjects) { subproject ->
    // 属性配置
    ext {
        junitVersion = "4.12"
        mockitoVersion = "2.12.0"
    }

    [compileJava, compileTestJava].each() {
        it.options.encoding = 'UTF-8'
        it.sourceCompatibility = 1.8
        it.targetCompatibility = 1.8
    }

    compileTestJava {
        options.compilerArgs += "-parameters"
    }

    dependencies {
        testCompile("junit:junit:${junitVersion}") {
            exclude group: 'org.hamcrest', module: 'hamcrest-core'
        }
        testCompile("org.mockito:mockito-core:${mockitoVersion}") {
            exclude group: 'org.hamcrest', module: 'hamcrest-core'
        }
    }
}

configure(rootProject) {
    description = "rpc-project"

    apply plugin: "groovy"

    configurations.archives.artifacts.clear()

    dependencies{
        compile(project(":rpc-registry"))
    }
}